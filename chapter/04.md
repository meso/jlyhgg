---
title: "Syntax in Functions - 関数におけるシンタックス"
layout: chapter
---



## Pattern matching - パターンマッチング

この商では、Haskellの便利なシンタックスを紹介しよう。まずはpattern matchingから。

pattern matchingは、あるデータが、与えられたpatternに一致しているか判定し、一致していればそのpatternにもとづいてデータを分解する、という一連の流れから構成される。

関数を定義するとき、関数のbodyをpatternごとに分けて定義することができる。このおかげで、関数を読みやすくかつ、簡潔にできる。そして、pattern matchingは、どんなtypeに対しても可能だ。数値、文字、リスト、タプル...などなど。

ここで、与えた数値が7か判定する関数を作ってみよう。

    lucky :: (Integral a) => a -> String
    lucky 7 = "LUCKY NUMBER SEVEN!"
    lucky x = "Sorry, you're out of luck, pal!"

関数`lucky`を呼び出すと、patternが上から下に向かって、順番に試される。そして、patternに一致する関数のbodyが見つかると、それが使われる。

最初のpatternに一致するのは、関数に与えた値が7のときだけだ。最初のpatternに一致しなければ、次のpatternが試される。残ったpatternは、すべての値に一致するから、値は`x`にbindされる。

ところで、関数`lucky` は、Starting Outで説明した`if`を使っても実現できそうだ。

    lucky' :: (Integral a) => a -> String
    lucky' x = if x == 7
              then "LUCKY NUMBER SEVEN!"
              else "Sorry, you're out of luck, pal!"

実際、`lucky'`は期待通りに動作する。でも、たとえば1から5の値を受け取った時はそれに対応するメッセージを表示して、そうでなければ`"Not between 1 to 5`と表示する関数を作るとしたらどうだろう?pattern matchingがなければ、一々if-elseで条件を書かなければいけない。

では、pattern matchingを使って、`sayMe`という関数を作ってみよう。

    sayMe :: (Integral a) => a -> String
    sayMe 1 = "One!"
    sayMe 2 = "Two!"
    sayMe 3 = "Three!"
    sayMe 4 = "Four!"
    sayMe 5 = "Five!"
    sayMe x = "Not between 1 and 5"

いいね！if-elseを使わずに、しかも読みやすい関数の定義ができた。なお、最後のパターン(すべての数値に一致するパターン)を先頭へ移動させると、後の1、2、...5というパターンには永遠に一致しなくなる。常に最初のパターンにマッチしてしまうからだ。

ところで、`関数`factorial`を作ったのを覚えているかな? 引数としてnを与えると、1からnまでの席を求める関数だ。あの関数は、再帰的にも定義できる。

まず、`factorial 0`が`1`になる、というところからはじめよう。そして、正の整数はすべて、それ自身とそのpredecessoをかけた数として定義できる。これをHaskellで書くと、次のようになる。

    factorial :: (Integral a) => a -> a
    factorial 0 = 1
    factorial n = n * factorial (n - 1)

おめでとう、君はいま、初めて関数を再帰的に定義した。催奇は、Haskellにおいて重要な部分だから、次の賞で詳しく説明する。ここでは、簡単に、関数がどう働くかを説明しよう。

例として、`factorial 3`は`3 * (factorial 2)`となる。`factorial 2`は`2 * factorial 1`となる。`factorial 2`は`2 * factorial 1`となる。`factorial 0`は`1`だから、すべてを合わせると`3 * 2 * 1 * 1`となる。

0の場合のパターンを先に書いたのは、これがなければ`factorial`の計算が永遠に再起し続けて、`0, -1, -2, ...`のように止まらなくなるからだ。催奇の定義では、催奇を停止させるパターンから先に定義して、その他のパターンを後に書くのが一般的だ。

さて、ここでパターンマッチングが失敗する例を紹介しよう。次の関数のパターンマッチングは不完全だ。

    charName :: Char -> String
    charName 'a' = "Albert"
    charName 'b' = "Broseph"
    charName 'c' = "Cecil"

この関数に、想定外の値を与えると、こうなる。

    ghci> charName 'a'
    "Albert"
    ghci> charName 'b'
    "Broseph"
    ghci> charName 'h'
    "*** Exception: tut.hs:(53,0)-(55,21): Non-exhaustive patterns in function charName

このエラーは、与えた値にマッチするパターンが見つからなかったことを報告している。パターンを作るときは、プログラムがクラッシュしないように毎回必ずすべての値にマッチするパターンを含め無くてはならない。

    charName' :: Char -> String
    charName' 'a' = "Albert"
    charName' 'b' = "Broseph"
    charName' 'c' = "Cecil"
    charName'  x  = "Anonymous"

これで、エラーは起こらなくなる。

    ghci> charName' 'h'
    "Anonymous"

パターンマッチングはタプルに対しても使うことができる。ペアとして表現されるベクトルを2つ受け取り、２次元の平面上でそのベクトルを足す、という関数を考えてみよう。ベクトルAのxとべくとるBのx、ベクトルAのyとべくとるB のyをそれぞれ足すことで、これは実現できる。

もし、パターンマッチを知らなければ、関数を次のように定義していただろう。

    addVectors :: (Num a) => (a, a) -> (a, a) -> (a, a)
    addVectors a b = (fst a + fst b, snd a + snd b)

もちろん、これは正しく動作するし何の問題もない。だけど、パターンマッチングを使えば、もっとうまく書ける。

    addVectors' :: (Num a) => (a, a) -> (a, a) -> (a, a)
    addVectors' (x1, y1) (x2, y2) = (x1 + x2, y1 + y2)

こっちのほうが分かりやすい。しかも、既にすべてのパターンにマッチしている。ということで、2つのペアを引数として受け取る、というのが保証できる。

さて、`fst`と`snd`は、それぞれペアから最初の値と、残りの値を抜き出す関数だ。でも要素が3つあるタプルに対しては、`fst`や`snd`は使えない。どうする? 自分で作ろう!

    first :: (a, b, c) -> a
    first (x, _, _) = x

    second :: (a, b, c) -> b
    second (_, y, _) = y

    third :: (a, b, c) -> c
    third (_, _, z) = z

ところで、List comprehensionで説明した`_`の意味を覚えているかな? `_`というのは、この変数には意味が無い、使われない、というのを表すのだった。そうそう、list comprehensionでもパターンマッチングが使える。試してみよう。

    ghci> let xs = [(1,3), (4,3), (2,4), (5,3), (5,6), (3,1)]
    ghci> [a+b | (a,b) <- xs]
    [4,7,6,8,11,4]

パターンマッチングが失敗すると、単に次のパターンへ移動する。

もちろん、パターンマッチングの中でリストを使うことだってできる。`[]`は、空のリストにマッチするし、`:`を使って、どんなリストにもマッチさせることができる。`[1,2,3]`というのは、`1:2:3:[]`のシンタックスシュガーだ、というのを覚えているかな? この記法を使うことで、たとえば`x:xs`とすれば、ある要素を先頭の要素と残りのリストに分解して、xとxsにbindすることができる。1つしか要素を持たないリストの場合は、たとえば`1:[]`のように分解され、それぞれ`x`には`1`が、`xs`には`[]`がbindされる。

Note: `x:xs`というのは多くの場面で、特に催奇で使用される。`x:xs`は要素数が1以上のリストに対してマッチする。もし、リストの最初の3津と残りを変数にbindしたければ、`x:y:z:zs`と書けばいい。これは要素数が3以上のリストにマッチする。

メモ: リストに対するパターンマッチングで`x:xs`ではなく`(x:xs)`と、かっこで包んでいますが、これはパターンマッチング用のシンタックスでありタプルではないです。

さて、リストに対するパターンマッチングの方法が分かったから、自分で`head`を定義してみよう。

    head' :: [a] -> a
    head' [] = error "Can't call head on an empty list, dummy!"
    head' (x:_) = x

動いているか確認。

    ghci> head' [4,5,6]
    4
    ghci> head' "Hello"
    'H'

期待通り!

次は、リストの先頭の要素が何か教えてくれる、ちょっとした関数を作ってみよう。

    tell :: (Show a) => [a] -> String
    tell [] = "The list is empty"
    tell (x:[]) = "The list has one element: " ++ show x
    tell (x:y:[]) = "The list has two elements: " ++ show x ++ " and " ++ show y
    tell (x:y:_) = "This list is long. The first two elements are: " ++ show x ++ " and " ++ show y

この関数は、空のリスト、singleton list、要素が2つのリスト、要素が2つより大きいリストに対してpattern matchingを行っているから安全だ。

NOTE `(x:[])`と`(x:y:[])`は、それぞれシンタックスシュガーを利用して、`[x]`と、`[x,y]`と書きなおすことができる。ただし、`(x:y:_)`を`[x,y,_]`とか空いてしまうと、これは要素が3つのリストのみにマッチするので注意。

    foo :: [a] -> String
    foo []      = "I'm an empty list."
    foo [x]     = "I'm a singleton list."
    foo [x,y]   = "I'm a list that contains 2 elems."
    foo [x,y,_] = "I'm a list that contains 3 elems."
    foo (x:xs) = "I'm a list that contains more than 3 elems."

これは期待通りに動作する。

    ghci> foo []
    "I'm an empty list."
    ghci> foo [1]
    "I'm a singleton list."
    ghci> foo [1,2]
    "I'm a list that contains 2 elems."
    ghci> foo [1,2,3]
    "I'm a list that contains 3 elems."
    ghci> foo [1,2,3,4]
    "I'm a list that contains more than 3 elems."

`length`関数は、既に自分で定義した。ここで、催奇とpattern matchingを使って、もう一度作ってみる。

    length' :: (Num b) => [a] -> b
    length' [] = 0
    length' (_:xs) = 1 + length' xs

これは`factorial`関数に似ている。まずは催奇が止まるように空のリストに対するパターンマッチングを書く。ちなみにこれをedge conditionという。次に、リストを先頭と残りの要素に分割して、、残りの要素に対してlength関数を再帰的に実行する。パターンマッチングが失敗しないよう、すべてのリストに対するパターンが用意されていることにも注目しよう。`[]`は空のリストに、`(_:xs)`は要素の数に関係なく、すべてのリストにマッチする。

関数lengthをcallすると何が起こるのか、追跡してみよう。

`[1,2,3]`を与えた場合、まずはリストを`[1]`と`[2,3]`に分割する。`[1]`の要素数は1だから、`1 + [2,3]の要素数`を返せば答えになる。では`[2,3]`の要素数は、というと、`[2]`と`[3]`に分割できるから、これも`1 + [2]の要素数`となる。残った`[3]`の要素数は、`[3]`と`[]`に分割できるから、`1 + 0`となる。結局、`[1,2,3]`の要素数は、`1 + (1 + (1 + 0))`を求めることになるから、3という正しい答えが得られる。

次は、関数`sum`を自分で定義してみよう。`sum []`は0になるから、まずはそのpatternを書いて、残りのpatternを加工。

    sum' :: (Num a) => [a] -> a
    sum' [] = 0
    sum' (x:xs) = x + sum' xs

パターンマッチングの中で使えるパターンのようなものもある。パターンにもとづいて分割したものに対し、それらすべてを含むもの、として別名をつけることができる。これはパターンの前に`@`を置くことで実現できる。例えば、`xs@(x:y:ys)`と書いた場合、`xs`は`(x:y:ys)`と等しい。以下に、簡単な例を示す。

    capital :: String -> String
    capital "" = "Empty string, whoops!"
    capital all@(x:xs) = "The first letter of " ++ all ++ " is " ++ [x]

試してみよう。

    ghci> capital "Dracula"
    "The first letter of Dracula is D"

通常、このパターンは、function bodyの中で分割されたパターンを分割される前の状態で取り出したい時に使う。これで、パターンを繰り返し書くのを防ぐことができる。

それと、`++`はパターンマッチングで使えないことに注意してほしい。仮に`x ++ y`があったとして、どこまでが、先頭で、どこまでが残りの部分になるんだろう? ということで、例えば、`(xs ++ [x,y,z])`や`(xs ++ [x])`はどうかというと、やはりリストの性質上、使うことはできない。



## Guards, guards! - ガードで防げ!

パターンマッチングは、値がある構造にもとづいているか調べ、分解する方法であるのに対し、ガードは、ある値、あるいはいくつかの値のpropertyがTrueかFalseかをテストする方法を提供する。これは、if文にとても似ているように思える。ガードの場合、複数の条件をより読みやすく記述でき、パターンマッチングとの相性もいい。

メモ: 値は値だろうと思っていましたが、property of valueという言い方をしているので　Haskellでは、値の中身と値がどのように作られたか、(どのパターンにマッチするか)、という構造をふくめて値と読んでいるのでしょうか。

詳しい説明をする前に、とりあえずガードを使った簡単な関数を定義してみよう。

これから定義するのは、BMI(Body Math Index)にもとづいて、罵詈雑言を返してくれる素敵な関数だ。BMIは自分の体重を、身長(単位はメートル)の2乗で割ることで求めることができる。BMIが`BMI <= 18.5`のときは痩せすぎ、`BMI > 18.5 && BMI <= 25`のときは標準、`BMI > 25 && BMI <= 30`のときは太り気味、`BMI > 30`のときは肥満、となる。早速、BMIを計算して結果を教えてくれる関数を定義しよう。

    bmiTell :: (RealFloat a) => a -> String
    bmiTell bmi
        | bmi <= 18.5 = "You're underweight, you emo, you!"
        | bmi <= 25.0 = "You're supposedly normal. Pffft, I bet you're ugly!"
        | bmi <= 30.0 = "You're fat! Lose some weight, fatty!"
        | otherwise   = "You're a whale, congratulations!"

ガードは関数の名前に続けて`|`で表し、その後にパラメータを置く。通常、ガードはインデントを行い、`|`の位置を揃える。(インデントは必ず` `が1つ以上ないと、エラーになる。)

ガードの条件は、それが`True`になれば、対応するbodyが使われる。`False`になれば、次のガードを調べる、というのを最後まで繰り返す。例えば、この関数に`24.3`を与えると、まず最初のガードで、18.5より小さい、あるいは等しいか判定される。これはFalseになるから、次のガードへ進む。次は25より小さいか等しい、という条件だから、これに対応する文字列が返される。

命令型の言語で同じことをしようとすると、if-elseを一々書く必要があるのに対し、ガードを使えば依り読みやすく記述できる。そして、個々の条件にあわせてif-elseを追加していくと、いずれ破綻するだろう。ガードはその代替手段として有効だ。

多くの場合、最後のガードはotherwiseとなる。`otherwise`は単に`otherwise = True`と定義されており、命令言語の`switch`文における`default`と同じ働きをしてくれる。

    ghci> otherwise
    True

ガードはパターンマッチングに似ている。パターンマッチングの場合は、パターンを満たしているか判定するのに対し、ガードは`True`か`False`を判定する。

もし、`otherwise`が用意されていなければ、すべてのガードが`False`になった場合、次のパターンへ進むことになる。これがガードとパターンマッチングの相性がいい理由だ。ただし、最終的にどのガードにも当てはまらなかった場合、エラーとなるので注意してほしい。

もちろん、複数のパラメータを受け取る関数でもguardを使うことができる。先ほどのBMI関数を、事前にBMIを計算してもらう代わりに、身長と体重を受け取って計算するように変更してみよう。

    bmiTell :: (RealFloat a) => a -> a -> String
    bmiTell weight height
        | weight / height ^ 2 <= 18.5 = "You're underweight, you emo, you!"
        | weight / height ^ 2 <= 25.0 = "You're supposedly normal. Pffft, I bet you're ugly!"
        | weight / height ^ 2 <= 30.0 = "You're fat! Lose some weight, fatty!"
        | otherwise                 = "You're a whale, congratulations!"

さあ、自分のBMIを調べてみよう。

    ghci> bmiTell 85 1.90
    "You're supposedly normal. Pffft, I bet you're ugly!"

わーい、(Haskellに嫌味を言われたけど...)、太ってなかった。

Note: ガードを使うときは、パラメータの後に`=`は書かない。Haskellに慣れていないと、よく間違えてシンタックスエラーになるので注意しよう。

もう一つの例として、`max`関数を自分で実装してみよう。関数`max`は、2つのvalueを受け取り、大きい方を返すのだった。

    max' :: (Ord a) => a -> a -> a
    max' a b
        | a > b     = a
        | otherwise = b

ガードはもちろん1行にまとめて書くこともできる。これまで開業していたのは読みやすさを優先していたからだ。小さな関数であれば、1行にまとめて書いても問題ない。関数`max`のガードを1行で書くと、こうなる。

    max' :: (Ord a) => a -> a -> a
    max' a b | a > b = a | otherwise = b

うーん、あんまり読みやすくない...。ということで、1行にまとめて書くのはおすすめしない。

続いて、`compare`関数を自分で定義してみよう。


    myCompare :: (Ord a) => a -> a -> Ordering
    a `myCompare` b
        | a > b     = GT
        | a == b    = EQ
        | otherwise = LT

では、試してみよう。

    ghci> 3 `myCompare` 2
    GT

Note: 関数を定義するとき、infix functionのほうが読みやすい場合は、`\``を使って、定義することもできる。たとえば、次のように関数を定義できる。

    ghci> let x `plus` y = x + y
    ghci> 1 `plus` 2
    3



## Where!? - whereはどこに!?

先程、以下の様なBMIを計算する関数を定義した。

    bmiTell :: (RealFloat a) => a -> a -> String
    bmiTell weight height
        | weight / height ^ 2 <= 18.5 = "You're underweight, you emo, you!"
        | weight / height ^ 2 <= 25.0 = "You're supposedly normal. Pffft, I bet you're ugly!"
        | weight / height ^ 2 <= 30.0 = "You're fat! Lose some weight, fatty!"
        | otherwise                   = "You're a whale, congratulations!"

この関数で同じことを3回も繰り返しているのに気づいたかな? 3回も! 3回も!

プログラミングでは、繰り返しを避けるのが望ましい。この場合は、同じexpressionを何度も書くのではなく、BMIの計算結果を、適当な変数にbindして、使い回すべきだ。では、この関数を以下のように変更してみよう。

    bmiTell :: (RealFloat a) => a -> a -> String
    bmiTell weight height
        | bmi <= 18.5 = "You're underweight, you emo, you!"
        | bmi <= 25.0 = "You're supposedly normal. Pffft, I bet you're ugly!"
        | bmi <= 30.0 = "You're fat! Lose some weight, fatty!"
        | otherwise   = "You're a whale, congratulations!"
        where bmi = weight / height ^ 2

通常、`where`はガードの後に書く。`where`のインデント位置は`|`と同じか、それ以上深くインデントするのがベターだ。そして、`where`に続けて変数や関数を定義する。

このとき、`where`で定義した変数や関数は、ガードの中で参照することができる。もし、BMIの計算式を少し変更したくなったら、`where`で定義した部分だけを変更すればいい。そして、これはコードの読みやすさを向上させるだけではなく、一度計算した結果を使いまわすことで、実効速度も向上する。では、ちょっと調子に乗って、次のように関数を変更してみよう。

    bmiTell :: (RealFloat a) => a -> a -> String
    bmiTell weight height
        | bmi <= skinny = "You're underweight, you emo, you!"
        | bmi <= normal = "You're supposedly normal. Pffft, I bet you're ugly!"
        | bmi <= fat    = "You're fat! Lose some weight, fatty!"
        | otherwise     = "You're a whale, congratulations!"
        where bmi = weight / height ^ 2
              skinny = 18.5
              normal = 25.0
              fat = 30.0

関数内のwhereで定義した変数は、その関数内でしか参照できないので、他の関数のnamespaceを汚染してしまう心配はない。変数名や関数名は、1行に1つしか定義できないので注意してほしい。そうでなければ、同じブロック内に定義されたものと解釈されなくなる。

whereは異なるpatternに対して、関数の定義を跨いで使うこともできる。もし、幾つかのパターンから、ある関数を共通に使いたい場合、グローバルに定義する必要がある。

`where`は、もちろんパターンマッチングにも使うことができる。先ほどの関数は、以下のようにも定義できる。

    ...
    where bmi = weight / height ^ 2
          (skinny, normal, fat) = (18.5, 25.0, 30.0)

ここで、First NameとLast Nameを与えると、イニシャルを変えいてくれる関数を定義してみよう。

    initials :: String -> String -> String
    initials firstname lastname = [f] ++ ". " ++ [l] ++ "."
        where (f:_) = firstname
              (l:_) = lastname

関数のパラメータに対して直接パターンマッチングができるので、完結でわかりやすくなる。

`where`ブロック内で定数を定義したように、関数を定義することもできる。健康を維持したままプログラミングができるよう、体重と身長を含むタプルを要素に持つリストを受け取って、BMIのリストを返す関数を定義してみよう。

    calcBmis :: (RealFloat a) => [(a, a)] -> [a]
    calcBmis xs = [bmi w h | (w, h) <- xs]
        where bmi weight height = weight / height ^ 2

この例でbmi関数を先に紹介したのは、関数のパラメータからbmiを直接計算することはできないからだ。BMIの計算をしてくれるヘルパー関数を先に定義して、それから全てのペアに対してそのヘルパー関数を使う必要があった。

そして、`where`はネストすることもできる。これは一般的なイディオムで、ちょっとしたヘルパー関数を定義したい時に`where`句を使う。もちろん、そのヘルパー関数内でも`where`句を使ってヘルパー関数のヘルパー関数を定義することもできる。

メモ: 上記のBMIを計算する関数、`bmi weight height = weight / height ^ 2`のように、ちょっとした計算をする関数のことを、一般的にヘルパー関数と呼んだりします。



## Let it be - letにしよう

`where`とよく似ているのが`let`だ。

`where`は関数の末尾で変数を定義して、ガードを含め、すべての関数から使えるようにする。それに対し`let`は、`let`自身がexpressionであり、他の関数から変数を参照することはできないので局所的な変数の定義に使うことができる。Haskellでは値に名前をつける時に`let`をよく使う。また、パターンマッチングにも使うことができる。

では、`let`を使ってみよう。以下は半径rと高さhを与えて、円筒の表面積を求める関数だ。

    cylinder :: (RealFloat a) => a -> a -> a
    cylinder r h =
        let sideArea = 2 * pi * r * h
            topArea = pi * r ^2
        in  sideArea + 2 * topArea

`let`がexpressionであることに注目してほしい。`let`で定義した変数や関数は、`in`というキーワードの後に使うことができる。今までに見たとおり、これは`where`を使っても同じことが実現できる。

Note let`で定義する変数名は1列に整列しよう。

では、`let`と`where`の違いはなんだろう? `let`は書いたその場で変数を定義して使うことができるのに対して、`where`は関数の後ろの部分で変数を定義して使う、という違いしかないように思える。

両者の違いは、`let`自身がexpressionである、というところだ。`where`はあくまでシンタックスだ。`let`と同様に`if`もexpressionであり、あらゆる場所で使うことができる、というのを説明したことを覚えているかな?

    ghci> [if 5 > 3 then "Woo" else "Boo", if 'a' > 'b' then "Foo" else "Bar"]
    ["Woo", "Bar"]
    ghci> 4 * (if 10 > 5 then 10 else 0) + 2
    42

`let`も同様にexpressionとして使うことができる。

    ghci> 4 * (let a = 9 in a + 1) + 2
    42

`let`を使って、関数にローカルスコープを作り出すこともできる。

    ghci> [let square x = x * x in (square 5, square 3, square 2)]
    [(25,9,4)]

幾つかの変数を定義したい場合、複数行に分けて書くと読みやすい。しかし、1行で書きたい場合もある。そのときはセミコロン`;`で区切って変数や関数を定義する。

    ghci> (let a = 100; b = 200; c = 300 in a*b*c, let foo="Hey "; bar = "there!" in foo ++ bar)
    (6000000,"Hey there!")

最後の変数には`;`を付ける必要はない。しかし、書くこともできる。先程述べたように、`let`をパターンマッチングと組み合わせて使うこともできる。`let`は、タプルを分解して各要素ごとに別名を付けて取り出す、という便利な使い方ができる。

    ghci> (let (a,b,c) = (1,2,3) in a+b+c) * 100
    600

もちろん、`let`をlist comprehensionと組み合わせて使うこともできる。体重と身長を持つペアを含むリストを受け取って、BMIを計算した結果を返す関数を、`where`ではなく`let`をlist comprehensionの中で使って書きなおしてみよう。

    calcBmis :: (RealFloat a) => [(a, a)] -> [a]
    calcBmis xs = [bmi | (w, h) <- xs, let bmi = w / h ^ 2]

`let`をlist comprehensionの中だけではなく、関数のpredicateの中でも使うことができる。`let`をlist comprehensionの中で使ったときは、その変数はoutput function(`|`の後ろの部分)からも参照することができる。ということで、太った人のみBMIの値を返す、という関数を作ることができる。

    calcBmis :: (RealFloat a) => [(a, a)] -> [a]
    calcBmis xs = [bmi | (w, h) <- xs, let bmi = w / h ^ 2, bmi >= 25.0]

`let`より先に定義されるため、`bmi`は`(w, h) <- xs`という部分では参照できない。

また、list comprehensionでは`in`というのを省略していることに注目してほしい。`let`で定義した変数や関数を使えるのは`in`の後だった。関数`calcBmis `では、`bmi`という変数を事前に定義しているので`in`は必要ない。しかし、`let`を関数のpredicateの中で定義して、predicateの中だけで有効な、predicateの外から参照できない変数を定義することもできる。

関数や定数を定義するとき、`in`と、その後に続く部分は、ghciを使っている時にも省略できる。`let`で定義したものは、ghciを終了するまで使うことができる。

    ghci> let zoot x y z = x * y + z
    ghci> zoot 3 9 2
    29
    ghci> let boot x y z = x * y + z in boot 3 4 2
    14
    ghci> boot
    <interactive>:1:0: Not in scope: `boot'

`let`が便利なのは分かった。でも、それなら`where`は必要ないんじゃない? しかし、`let`はexpressionであり、ガードをまたがってグローバルに変数を定義できないことを思い出してほしい。そして、関数を定義するときに`where`を使ったほうが、その関数で使う変数やヘルパー関数が一覧できてわかりやすい、という人もいる。その場合、関数のtype declarationとfunction bodyが近くにあるほうがより読みやすくなる。



## Case expressions - case式

CやC++、Javaなどの命令形言語でのプログラミング経験がある方は、caseがどんなものかご存知だろう。caseは変数を受け取り、特定の値に対するコードのブロックを実行し、caseに当てはまらなかった場合に実行されるブロックを含むこともある。

Haskellの`case`は、命令型のcaseをちょっとよくしたものだ。名前が暗示している通り、Haskellの`case`は、`let`や`if`と同様、expressionだ。変数の値に基づいて、特定のコードを実行する、というのは`case`だけではなく、パターンマッチングでも同じことをしている。

...おや、変数を受け取って、パターンマッチングして、その変数に基づいてコードの断片を実行する、というのは聞いたことがあるぞ? 関数を定義するときの、パラメータに対するパターンマッチングだ。

そう、実は関数を定義した時に使うパターンマッチは、`case`のシンタックスシュガーだったのだ。ということで、以下のコードは等しいので、両者を交換することができる。

    head' :: [a] -> a
    head' [] = error "No head for empty lists!"
    head' (x:_) = x

    head' :: [a] -> a
    head' xs = case xs of [] -> error "No head for empty lists!"
                          (x:_) -> x

ご覧のとおり、`case`のシンタックスは単純だ。

    case expression of pattern -> result
                       pattern -> result
                       pattern -> result

expressionはパターンに対してマッチする。パターンマッチの挙動は、最初にマッチしたものを使う、という予想通りの挙動をする。すべてのパターンを試した結果、どれにもマッチしなければ、ランタイムエラーとなる。

関数のパターンマッチは、関数の定義でしか使えないのに対して、`case`のパターンマッチは、あらゆる場所で使うことができる。例えば、以下の様なことができる。

    describeList :: [a] -> String
    describeList xs = "The list is " ++ case xs of [] -> "empty."
                                                   [x] -> "a singleton list."
                                                   xs -> "a longer list."



`case`はexpressionの途中で、パターンマッチをすることができるので便利だ。関数定義でのパターンマッチは`case`のシンタックスシュガーだったから、以下のように関数を定義することもできる。

    describeList :: [a] -> String
    describeList xs = "The list is " ++ what xs
        where what [] = "empty."
              what [x] = "a singleton list."
              what xs = "a longer list."

