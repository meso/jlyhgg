---
title: "Modules - モジュール"
layout: chapter
---



## Loading modules - モジュールを読み込む

Haskellのモジュールはtypeとtypeclassに関係する関数の集まりだ。Haskellのプログラムは、メインモジュールが他のモジュールを読み込んでそこに定義されている関数を使ってなにかする、というものになる。コードをモジュールに分割する利点は大きい。モジュールが十分に一般的であれば、その関数を他のプログラムで使い回すことができる。もし、あなたのコードがお互いに依存しないモジュールに分割できるなら、後からそれらを再利用できる。目的別にコードを分割することで、管理しやすくかつ特定の問題に対する答えを導きやすくなる。

Haskellの標準ライブラリはモジュールに分割されており、それらのモジュールには、typeや一般的な目的別の関数が含まれている。たとえばリストを操作するためのモジュールや、並行処理のためのモジュール、複素数を扱うモジュールなどがある。いままで扱ってきた関数や、type、typeclassはPreludeという標準で組み込まれているモジュールの一部だった。この商では、いくつか役に立つモジュールと、それに含まれる関数を紹介する。まずは、モジュールの読み込み方法から説明する。

Haskellのスクリプトからモジュールを読み込むためのシンタックスは、`import <module name>`だ。これは他のどの関数の定義より先に行う必要があるので、通常、ファイルの一番先頭に`import ...`というのを記述する。もちろん、ひとつのスクリプト内で複数のモジュールを読み込むことができる。
`import`というのを複数行に記述するだけだ。

では、`Data.List`というモジュールを読み込んでみよう。このモジュールは、リストに対する操作を提供する便利な関数が含まれており、たとえば、リストからユニークな値がいくつあるかを教えてくれる関数などがある。

    import Data.List
    
    numUniques :: (Eq a) => [a] -> Int
    numUniques = length . nub

インポートされたモジュールに含まれる関数は、グローバルなネームスペースで参照できるようになるので、スクリプト内のどこからでもそれらの関数を使えるようになる。関数`nub`は、受け取ったリストから重複した要素を刈り取ったリストを返すので、`length.nub`という関数合成は、リストが持つユニークな要素の数を返す関数になる。

もちろん、ghciでもグローバルにモジュールを読み込むことができる。ghciの中で、`Data.List`の関数を使いたければ、こうする。

    ghci> :m + Data.List

ghciの中で複数のモジュールを読み込みたければ、一度に複数のモジュールを読み込むことができるので、何度も`:m`をタイプする手間を省ける。

    ghci> :m + Data.List Data.Map Data.Set

もちろん、一度モジュールを読み込んだ後は、`:m`を入力する必要はない。モジュール内の特定の関数をいくつか取り出し使いたければ、モジュール名の後に関数名をタプルの形式で書く。

    import Data.List (nub, sort)

ある特定の関数を除く、すべての関数をモジュールから読み込む、というのも可能だ。すでにモジュール内に存在する関数と同じ名前の関数を定義していて、名前の衝突を避けたい場合にこの機能が役立つ。たとえば、既に`nub`という関数を定義していて、その関数名を除くすべての関数を`Data.List`から読み込みたい場合は、以下のように記述する。

    import Data.List hiding (nub)

他に名前の衝突を避ける方法として、qualified  importというのがある。たとえば、ghciを起動すると最初に読み込まれるPreludeモジュールには`filter`や`map`という関数が定義されている。しかし、`Data.Map`というキーからバリューを求めるような関数を集めたモジュールにも同じ名前の関数が定義されている。このままでは、PreludeかData.Mapか、どちらで定義された関数を使うべきか判断できない。そこで、以下のように解決する。

    import qualified Data.Map

これで、最初から読み込まれている`filter`関数はそのままに、Data.Mapモジュールに組み込まれているfilter関数は、`Data.Map.filter`として使えるようになる。ただ、このままでは毎回`Data.Map.`というプレフィックスをつけることになって面倒なので、読み込まれる関数の名前を別の短い名前に変更したほうが便利だ。

    import qualified Data.Map as M

これで、`Data.Map.filter`を`M.filter`として使える。

Haskellの知識を深めるには、標準ライブらいに定義されている関数を概観するのが手っ取り早く勝つ有効な手段となる。もちろん、それぞれのモジュールのソースコードを見ることができる。他のモジュールのソースコードをみて学ぶというのは、自分でモジュールを書くときの手助けとなるだろう。

どこに目的の関数が定義されているかを調べるには、Hoogleを使うのが便利だ。これは、関数名やモジュール名、type signatureから目的の関数を探すための検索エンジンだ。



## Data.List

`Data.List`はリストに関するモジュールで、リストを操作するための便利な関数が含まれている、実は既にそのモジュールに含まれる関数を幾つか使っている。`map`や`filter`は、その例だ。Preludeモジュールでは利便性を考慮していくつかの関数を`Data.List`からあらかじめ読み込んでいる。そのため、改めて`Data.List`をimportする必要はない。

では、まだ使ったことのない関数を幾つか紹介しよう。

`intersperse`関数は、ある要素とリストを受け取り、リストの要素と要素の間に受け取った要素を挟んだリストを返す、という関数だ。試してみよう。

    ghci> intersperse '.' "MONKEY"
    "M.O.N.K.E.Y"
    ghci> intersperse 0 [1,2,3,4,5,6]
    [1,0,2,0,3,0,4,0,5,0,6]

`intercalate `関数は、リストのリスト、とリストを受け取って、そのリストを、リストのリストのそれぞれの間に挟んで、最後にそのリストをflattenしたリストを返す、という関数だ。

    ghci> intercalate " " ["hey","there","guys"]
    "hey there guys"
    ghci> intercalate [0,0,0] [[1,2,3],[4,5,6],[7,8,9]]
    [1,2,3,0,0,0,4,5,6,0,0,0,7,8,9]

`transpose`関数はリストのリストを受け取ってtransposeする関数だ。2次元をリストで表現した場合、その行と列を入れ替えたリストが返される。つまり、行列にたいする転置行列を得る関数だ。

    ghci> transpose [[1,2,3],[4,5,6],[7,8,9]]
    [[1,4,7],[2,5,8],[3,6,9]]
    ghci> transpose ["hey","there","guys"]
    ["htg","ehu","yey","rs","e"]

たとえば、3次元の多項式があったとして、$3x^2 + 5x + 9$、$10x^3 + 9$、$8x^3 + 5x^2 + x - 1$を足したいとする。これをHaskellのリストで表現すると`[0,3,5,9], [10,0,0,9], [8,5,1,-1]`となる。そして、これらを足すにはこのようにする。

    ghci> map sum $ transpose [[0,3,5,9],[10,0,0,9],[8,5,1,-1]]
    [18,8,6,17]

それぞれの行と列が入れ替わるので、目的の多項式が得られる。この場合、変数xの係数が次元の高い順に3,2,1,0と並んでいたのが、3,3,3,3,、2,2,2,2、...という順に入れ替わることになるので、結果としてその次元の係数を足し合わせることになる。　

`foldl'`と、`foldl1'`は、foldrとfoldlをより厳密にしたものだ。巨大なリストに対してfoldをすると、頻繁にスタックオーバーフローが起こる。この問題の原因は、実際にaccumulatorが計算されるのはその対象がfoldされる時点になってから、という特性による。実際にfoldで何が起こっているのかというと、accumulatorは、必要になったら計算された結果を返す、という約束のようなものを生成している。ちなみに、これはthunkとも言う。つまり、このthunkが計算の途中で大量に生成されてしまい、結果としてスタックを使い果たすことになるのだ。strict foldは、スタックを食い尽くす代わりに、計算の途中結果を逐一計算していく。foldを使っていてスタックオーバーフローに出くわしたら、`foldr'`や`foldl'`を試そう。

`concat`はリストのリストをリストへと平坦化する。

    ghci> concat ["foo","bar","car"]
    "foobarcar"
    ghci> concat [[3,4,5],[2,3,4],[2,1,1]]
    [3,4,5,2,3,4,2,1,1]

`concat`が平坦化するのは1段階までだから、リストのリスト...、のリストを完全に平坦化したい場合は`concat`を複数回行う必要がある

`concatMap`は、まず与えた関数をリストにマッピングしてから`concat`を行う。

    ghci> concatMap (replicate 4) [1..3]
    [1,1,1,1,2,2,2,2,3,3,3,3]

`and`は真偽値のリストを受け取り、すべてTrueだった場合にTrueを返す。

    ghci> and $ map (>4) [5,6,7,8]
    True
    ghci> and $ map (==4) [4,4,4,3,4]
    False

`or`は、`and`のようにBoolのリストを受け取り、どれかがTrueであればTrueを返す。

    ghci> or $ map (==4) [2,3,4,5,6,1]
    True
    ghci> or $ map (>4) [1,2,3]
    False

`any`と`all`はpredicateとリストを受け取り、文字通りある要素、あるいはすべての要素がpredicateを見たしたとき、`True`を返す。通常、predicateを`map`する代わりに`any`や`all`を使う。

    ghci> any (==4) [2,3,5,6,1,4]
    True
    ghci> all (>4) [6,9,10]
    True
    ghci> all (`elem` ['A'..'Z']) "HEYGUYSwhatsup"
    False
    ghci> any (`elem` ['A'..'Z']) "HEYGUYSwhatsup"
    True

`iterate`は、関数と初期値を受け取り、関数に初期値を適用した値、関数にその値を適用した値、関数に...という具合に、関数に初期値を適用した値をもとにして適用を続け、それを無限リストとして返す。

    ghci> take 10 $ iterate (*2) 1
    [1,2,4,8,16,32,64,128,256,512]
    ghci> take 3 $ iterate (++ "haha") "haha"
    ["haha","hahahaha","hahahahahaha"]

`splitAt`は、数値とリストを受け取り、その数値でリストを分割した結果をタプルとして返す。

    ghci> splitAt 3 "heyman"
    ("hey","man")
    ghci> splitAt 100 "heyman"
    ("heyman","")
    ghci> splitAt (-3) "heyman"
    ("","heyman")
    ghci> let (a,b) = splitAt 3 "foobar" in b ++ a
    "barfoo"

`takeWhile`は非常に便利な関数だ。`takeWhile`は、predicateが`True`の間、リストから要素を取り出し続け、predicateを満たさなくなった時点で、リストを切り取る。これがいかに便利か、見てみよう。

    ghci> takeWhile (>3) [6,5,4,3,2,1,2,3,4,5,4,3,2,1]
    [6,5,4]
    ghci> takeWhile (/=' ') "This is a sentence"
    "This"

ここで、3乗した結果が10000以下の数を総和するとしよう。`map (^3)`を無限リスト`[1..]`に適用すれば、3乗した結果の無限リストは得られる。しかし、`filter`が扱えるのは有限のリストのみだそこで、リストが昇順に並んでいることを利用して、`takeWhile`を使う。

    ghci> sum $ takeWhile (<10000) $ map (^3) [1..]
    53361

無限リストに対して`(^3)`を適用して、その後10000より大きい数値に遭遇したらその時点で無限リストを切り取る。これで、総和が簡単に求められる。

`dropWhile`も似たような関数で、こちらはpredicateが`True`の間、リストから要素を取り除き続ける。そして、predicateが`False`になった時点でのこりのリストを返す。これも愛すべき便利な関数だ。

    ghci> dropWhile (/=' ') "This is a sentence"
    " is a sentence"
    ghci> dropWhile (<3) [1,2,2,2,3,4,5,4,3,2,1]
    [3,4,5,4,3,2,1]

ここで、日付ごとの株価を表すリストを与えるとしよう。リストは、最初が株価、2番目が年、3番目が月、4番目が日にちを表すタプルで構成される。株価がはじめて1000ドルを超えたのはいつか、求めるとしよう。

    ghci> let stock = [(994.4,2008,9,1),(995.2,2008,9,2),(999.2,2008,9,3),(1001.4,2008,9,4),(998.3,2008,9,5)]
    ghci> head (dropWhile (\(val,y,m,d) -> val < 1000) stock)
    (1001.4,2008,9,4)

`span`も`takeWhile`と似たようなものだが、こちらはリストのペアを返す。ペアに含まれる最初のリストは、`takeWhile`にpredicateを与えて得られるリストと同じものを含み、2番目のリストは、残りのリストが含まれる。

    ghci> let (fw, rest) = span (/=' ') "This is a sentence" in "First word:" ++ fw ++ ", the rest:" ++ rest
    "First word: This, the rest: is a sentence"

`span`はpredicateが`True`の間、リストを切り取るのに対して、`break`は、predicateがはじめて`True`になった時点でリストを切り取る。つまり、`break p`というのは`span (not . p)`というのとおなじだ。

    ghci> break (==4) [1,2,3,4,5,6,7]
    ([1,2,3],[4,5,6,7])
    ghci> span (/=4) [1,2,3,4,5,6,7]
    ([1,2,3],[4,5,6,7])

`break`した結果、2番目のリストに含まれる先頭の要素は、predicateを満たした要素から始まる。

`sort`はその名の通り、リストをソートする。ただしソートするためにはリスト内の要素同士を比較する必要があるので、ソート対象のリストに含まれる要素は`Ord`typeclassに属していなければならない。

    ghci> sort [8,5,3,2,1,6,4,2]
    [1,2,2,3,4,5,6,8]
    ghci> sort "This will be sorted soon"
    "    Tbdeehiillnooorssstw"

`group`は、受け取ったリスト内の隣り合う要素が等しければ、それらをまとめたサブリストを返す。

    ghci> group [1,1,1,1,2,2,2,2,3,3,2,2,2,5,6,7]
    [[1,1,1,1],[2,2,2,2],[3,3],[2,2,2],[5],[6],[7]]

リストを`group`するまえに`sort`しておけば、各々の要素がリスト内で南海出現するか知ることができる。

    ghci> map (\l@(x:xs) -> (x,length l)) . group . sort $ [1,1,1,1,2,2,2,2,3,3,2,2,2,5,6,7]
    [(1,4),(2,7),(3,2),(5,1),(6,1),(7,1)]

`inits`と`tails`は、`init`と`tail`に似ているが、これらはリストから要素がなくなるまで再帰的に適用される。では、観察してみよう。

    ghci> inits "w00t"
    ["","w","w0","w00","w00t"]
    ghci> tails "w00t"
    ["w00t","00t","0t","t",""]
    ghci> let w = "w00t" in zip (inits w) (tails w)
    [("","w00t"),("w","00t"),("w0","0t"),("w00","t"),("w00t","")]

ここで、リストから目的のリストを検索する関数を、foldを使って実装してみよう。

    search :: (Eq a) => [a] -> [a] -> Bool
    search needle haystack =
        let nlen = length needle
        in  foldl (\acc x -> if take nlen x == needle then True else acc) False (tails haystack)

まずは、検索対象のリストにたいして`tails`を適用する。そして、`tails`した結果のリストにたいして、そのリストが目的のリストと同じ要素から始まっているかを調べる。

いま実装した関数の振る舞いは、`isInfixOf`と同じだ。`isInfixOf`は、検索対象のリスト内のどこかに目的のリストが存在すれば`True`を返すという関数だ。

    ghci> "cat" `isInfixOf` "im a cat burglar"
    True
    ghci> "Cat" `isInfixOf` "im a cat burglar"
    False
    ghci> "cats" `isInfixOf` "im a cat burglar"
    False

`isPrefixOf`と`isSuffixOf`は、それぞれ検索対象のリスト内の先頭、あるいは末尾に目的のリストが存在するか調べる。

    ghci> "hey" `isPrefixOf` "hey there!"
    True
    ghci> "hey" `isPrefixOf` "oh hey there!"
    False
    ghci> "there!" `isSuffixOf` "oh hey there!"
    True
    ghci> "there!" `isSuffixOf` "oh hey there"
    False

`elem`と`notElem`は、与えた要素が検索対象のリスト内に存在するか調べる。

`partition`は、predicateとリストを受け取り、リストのペアを返す。ペアに含まれる1番目のリストには、predicateを満たす要素が、2番目のリストにはpredicateを一度も満たさなかった要素が含まれる。

    ghci> partition (`elem` ['A'..'Z']) "BOBsidneyMORGANeddy"
    ("BOBMORGAN","sidneyeddy")
    ghci> partition (>3) [1,3,5,6,3,2,1,0,3,7]
    ([5,6,7],[1,3,3,2,1,0,3])

これが`span`や`break`とどう違うのかを理解するのは重要だ。　

    ghci> span (`elem` ['A'..'Z']) "BOBsidneyMORGANeddy"
    ("BOB","sidneyMORGANeddy")

`span`や`break`は、一度predicateを満たさない、あるいは満たす要素に遭遇した時点でリストを分割するのに対して、`partition`は、リスト内のすべての要素に対してpredicateを満たすかどうか判定し、その結果のリストをタプルとして返す。

`find`はpredicateとリストを受け取り、リストから最初にpredicateを満たした要素を返す。ただし、結果は`Maybe`に包まれたものとして返される。次の賞で、algebraic data typesについては詳しく説明するので、とりあえず今知っておくべきことは、`Maybe`で包まれた値は`Just その値`もしくは`Nothing`になる、ということだ。リストが空のリスト`[]`もしくはある要素を含むリストの両方を表現できるように、Maybeな値も、要素そのもの、もしくは要素がない状態、というのを表現できる。また、たとえば整数のリストのtypeは、`[Int]`になるように、Maybeな整数のtypeも`Maybe Int`になる。さて、話を戻して`find`を試してみよう。

    ghci> find (>4) [1,2,3,4,5,6]
    Just 5
    ghci> find (>9) [1,2,3,4,5,6]
    Nothing
    ghci> :t find
    find :: (a -> Bool) -> [a] -> Maybe a

`find`のtypeに注目してほしい。結果が`Maybe a`となっている。`Maybe a`とすることで、あるtypeの要素もしくは要素が存在しないということを表現できる。一方、リストを使えば空のリストで要素がないことを表現できるものの、1つ以上の要素を含むリストも作れてしまう。

ところで、株価が初めて1000ドルを超えた日付を求めたのを覚えているかな? `head (dropWhile (\(val,y,m,d) -> val < 1000) stock)`として求めたのだった。そして、`head`というのが安全ではない、というのも覚えているかな? もし、株価が1000ドルを超えなかったら何が起こるだろうか。`dropWhile`は空のリストを返し、空のリストに対する`head`はエラーを引き起こす。しかし、これを`find (\(val,y,m,d) -> val > 1000) stock`のように書きなおせば、より安全になる。もし株価が1000ドルを超えなかったとしても、つまり、predicateを満たす要素が存在しなくても、`Nothing`という結果が得られる。しかも、目的の要素が見つかった場合には、`Just (1001.4,2008,9,4)`という適切な結果が得られる。

`elemIndex`は、結果がBoolではない、というのを除いて`elem`と同じだ。もしリスト内に探している要素があれば、そのインデックスを`Maybe`で包んだ値として返すし、見つからなければ`Nothing`を返す。

    ghci> :t elemIndex
    elemIndex :: (Eq a) => a -> [a] -> Maybe Int
    ghci> 4 `elemIndex` [1,2,3,4,5,6]
    Just 3
    ghci> 10 `elemIndex` [1,2,3,4,5,6]
    Nothing

`elemIndices`は、`elemIndex`と似ているが、こちらはリスト内に目的の要素が複数ある場合、それらのインデックスをまとめてリストとして返す。ここでは、結果をインデックスのリストとして表現しているので`Maybe`を使う必要はない。空のリストが要素が見つからなかった、ということを表現できるため、`Nothing`と類似した働きができるのだ。

    ghci> ' ' `elemIndices` "Where are the spaces?"
    [5,9,13]

`findIndex`は、`find`と似ているが、最初にpredicateを満たした要素を`Maybe`な値として返す。`findIndices`は、リストからpredicateを満たす要素をすべて取り出し、リストとして返す。

    ghci> findIndex (==4) [5,3,2,1,6,4]
    Just 5
    ghci> findIndex (==7) [5,3,2,1,6,4]
    Nothing
    ghci> findIndices (`elem` ['A'..'Z']) "Where Are The Caps?"
    [0,6,10,14]


さて、`zip`と`zipWith`はもう学んだね。、どちらも2つのリストを受け取り、2つのパラメータを受け取るbinary functionによってタプルにまとめるのだった。でも、3つのリストをまとめたい、あるいは3つのパラメータを受け取る関数を使ってまとめたい場合はどうしよう?そうだね、実は`zip3`や`zip4`という関数がある。`zipWith3`、`zipWith4`もある。そして、これらは7まである。ただ、この方法は小細工に見える。もちろんこれらは機能するが、8つのリストをまとめる場面というのはそうそうないだろう。実は、リストを無限に受け取って、それらをまとめる賢い方法がある。しかし、その方法を知るにはまだ知識が足りない。

    ghci> zipWith3 (\x y z -> x + y + z) [1,2,3] [4,5,2,2] [2,2,3]
    [7,9,8]
    ghci> zip4 [2,3,3] [2,2,2] [5,5,3] [2,2,2]
    [(2,2,5,2),(3,2,5,2),(3,2,3,2)]

なお、普通の`zip`や`zipWith`と同様、`zip3`や`zipWith3`に与えたリストの長さが異なる場合、短い方に合わせて、残りの要素は取り除かれる。

`lines`は、ファイルなどからの読み込みを扱うのに便利な関数だ。これは、文字列を受け取り、行ごとの文字列としてリストに分割する。

    ghci> lines "first line\nsecond line\nthird line"
    ["first line","second line","third line"]

`'\n'`というのは、UNIXにおける開業を意味する。Haskellでは、バックスラッシュ`\`は特別な意味を持つ。

`unlines`は、`lines`と逆の働きをし、文字列のリストを受け取り、それらを文字列にまとめる。

    ghci> unlines ["first line", "second line", "third line"]
    "first line\nsecond line\nthird line\n"

`words`と`unwords`は、文字列を単語に区切り、単語のリストから文字列へまとめる、とても便利な関数だ。

    ghci> words "hey these are the words in this sentence"
    ["hey","these","are","the","words","in","this","sentence"]
    ghci> words "hey these           are    the words in this\nsentence"
    ["hey","these","are","the","words","in","this","sentence"]
    ghci> unwords ["hey","there","mate"]
    "hey there mate"

さて、`nub`は既に紹介した。リストから重複した要素を取り除き、要素の重複がないリストを返すという無味乾燥な関数だ。しかし、変な名前の関数だ。"nub"というのは、"a small lump or essential part of something"という意味だ。古の言葉より、現実的な関数の名前にすべきだと思うね。

    ghci> nub [1,2,3,4,3,2,1,2,3,4,3,2,1]
    [1,2,3,4]
    ghci> nub "Lots of words and stuff"
    "Lots fwrdanu"

`delete`は削除対象の要素とリストを受け取り、リスト内ではじめてその要素が出現した時、その要素を取り除いたリストを返す。

    ghci> delete 'h' "hey there ghang!"
    "ey there ghang!"
    ghci> delete 'h' . delete 'h' $ "hey there ghang!"
    "ey tere ghang!"
    ghci> delete 'h' . delete 'h' . delete 'h' $ "hey there ghang!"
    "ey tere gang!"

`\\`は、リストの差を求める関数だ。この関数は、差集合を求めるような関数であり、左側のリスト内のようその内、右側のリスト内に存在する要素をすべて取り除く。

    ghci> [1..10] \\ [2,5,9]
    [1,3,4,6,7,8,10]
    ghci> "Im a big baby" \\ "big"
    "Im a  baby"

`[1..10] \\ [2,5,9]`というのは、`delete 2 . delete 5 . delete 9 $ [1..10]`というのと同じだ。

`union`も同様に集合を扱う関数だ。これは、2つのリストの和集合を返す。2番目のリスト内の要素すべてにたいして、1番目のリスト内に存在するか調べ、まだ存在しなければ、その要素を1番目のリストへ追加する。ただし、2番目のリストの重複した要素は取り除かれるので注意が必要だ。

    ghci> "hey man" `union` "man what's up"
    "hey manwt'sup"
    ghci> [1..7] `union` [5..10]
    [1,2,3,4,5,6,7,8,9,10]

`intersect`は積集合のように振る舞う。どちらのリストにも存在する要素を返す関数だ。

    ghci> [1..7] `intersect` [5..10]
    [5,6,7]

`insert`は、ソート可能な要素と対象のリストを受け取り、その要素より小さいか等しい要素の隣に、その要素を挿入する。言い換えれば、`insert`は、リストの先頭から挿入したい要素より大きいか等しい要素を調べ続け、もし条件を満たす要素が見つかれば、その要素の前に目的の要素を挿入する。

    ghci> insert 4 [3,5,1,2,8,2]
    [3,4,5,1,2,8,2]
    ghci> insert 4 [1,3,4,4,1]
    [1,3,4,4,4,1]

最初の例では、`4`は、`3`と`5`の間に挿入され、2番目の例では、`3`と`4`の間に挿入されている。

もし、ソート済みのリストにたいして`insert`した場合、結果のリストもソート済みとなる。

    ghci> insert 4 [1,2,3,5,6,7]
    [1,2,3,4,5,6,7]
    ghci> insert 'g' $ ['a'..'f'] ++ ['h'..'z']
    "abcdefghijklmnopqrstuvwxyz"
    ghci> insert 3 [1,2,4,3,2,1]
    [1,2,3,4,3,2,1]


`length, take, drop, splitAt, !! replicate`、これらの関数は、パラメータとして、あるいは結果を返す際に`Int`を使用するが、`Int`の代わりにより汎用的なtypeclassである`Integral`や`Num`を受け取ったり返したりできたほうが、より便利に使えるだろう。`Int`を使用するのは、Haskellの歴史的な理由による。これらを修正すれば、既存のコードを破壊しかねない。そこで、`Data.List`には、これらをより汎用的にした`genericLength, genericTake, genericDrop, genericSplitAt, genericIndex, genericReplicate`が用意されている。たとえば、.`length`のtype signatureは、`length :: [a] -> Int`となっている。例えば、リスト内の要素の平均を求めようとして、`let xs = [1..6] in sum xs / length xs`とすると、`/`は`Int`に対して使用できないので、type errorが起こってしまう。一方、`genericLength`のtype signatureは、`genericLength :: (Num a) => [b] -> a`となっている。`Num`は、浮動小数としても振る舞うから、`let xs = [1..6] in sum xs / genericLength xs`としても大丈夫だ。

`nub, delete, union, intersect, group`より汎用的にした`nubBy, deleteBy, unionBy, intersectBy, groupBy`という関数が用意されている。両者の違いは、前者の関数が要素の比較に`==`という関数を使うのに対し、後者の関数は比較用の関数を受け取って要素を比較するところにある。つまり、`group`は`groupBy (==)`と等しい。


たとえば、ある関数の値を1秒ごとに記録したリストがあるとしよう。そして、値が0を下回った、あるいは0を超えた時にそれまでの値をまとめたいとしよう。普通の`group`を使った場合、隣り合うリスト同士でまとめられてしまう。しかし、ここで実現したいのは、ある値が負の数になったとき、あるいはそうではなくなった時にそれまでの要素をまとめる、というものだ。そこで、`groupBy`の出番となる。比較用の関数として、、2つの要素が成果不可を判定しそれらを比較する関数を与えると、、2つの要素がどちらも同じ基準を満たせば`True`となるので、負の数から正の数、あるいは正の数から負の数、という時点でリストをまとめることができる。

    ghci> let values = [-4.3, -2.4, -1.2, 0.4, 2.3, 5.9, 10.5, 29.1, 5.3, -2.4, -14.5, 2.9, 2.3]
    ghci> groupBy (\x y -> (x > 0) == (y > 0)) values
    [[-4.3,-2.4,-1.2],[0.4,2.3,5.9,10.5,29.1,5.3],[-2.4,-14.5],[2.9,2.3]]

これで、どの部分が正負なのか、はっきりした。2つの要素を受け取る比較用の関数は、どちらの要素も正あるいは負のとき`True`を返す。この比較用の関数は、最初の関数のほうが読みやすいと思うが、`\x y -> (x > 0) && (y > 0) || (x <= 0) && (y <= 0)`という書き方もできる。そして、よりわかりやすい比較用の関数を書く方法としては、`Data.Function`からインポートした`on`関数を使う方法がある。`on`は次のように定義されている。

    on :: (b -> b -> c) -> (a -> b) -> a -> a -> c
    f `on` g = \x y -> f (g x) (g y)

つまり、`(==) `on` (> 0)`というのは、`\x y -> (x > 0) == (y > 0)`という意味になる。たとえば、次のようなことができるので、`on`は、比較用の関数としてよく使われる。

    ghci> groupBy ((==) `on` (> 0)) values
    [[-4.3,-2.4,-1.2],[0.4,2.3,5.9,10.5,29.1,5.3],[-2.4,-14.5],[2.9,2.3]]

超わかりやすいね! これは、次のような意味になる。それぞれの要素が0より大きいか判定し、その結果が等しければ、その時点で要素をまとめる。

同様に、`sort, insert, maximum, minimum`を依り一般的にしたものがある。`groupBy`は、2つの要素を受け取り等しいかを判定する関数を受け取る。`sortBy, insertBy, maximumBy, minimumBy`は、1つの要素を受け取り、より大きい、等しい、あるいはより小さいか判定する関数を受け取る。`sortBy`のtype signatureは、`sortBy :: (a -> a -> Ordering) -> [a] -> [a]`となっている。`Ordering`が値として`GT, EQ, LT`を持つというのを覚えていれば、`sort`というのは、`sortBy compare`と等しいと考えていい。`compare`は、typeclassが`Ord`の値を2つ受け取り、判定結果を`Ordering`の値として返すからだ。

リストも比較可能だが、比較する際の順番は辞書順となる。もし、リストのリストに対してその中身ではなく、リストの長さに基づいてリストをソートするとしたら、どうだろう、そうだね、ご想像の通り`groupBy`関数を使うだろう。

    ghci> let xs = [[5,4,5,4,4],[1,2,3],[3,5,4,3],[],[2],[2,2]]
    ghci> sortBy (compare `on` length) xs
    [[],[2],[2,2],[1,2,3],[3,5,4,3],[5,4,5,4,4]]

すばらしい! `compare \`on\` length`というのは自然な英語のようだ。もし、これが何をしているのかよくわからなければ、`compare \`on\` length`というのは、`\x y -> length x `compare` length y`と等しい、と考えればいい。等しさを判定する関数を受け取る比較用の関数を扱う場合、通常、`(==) \`on\` something`というのがよく使われる。また、順序を扱う関数を`比較用の関数に渡す場合には`compare `on` something`というのをよく使うことになる。



## Data.Char

`Data.Char`モジュールは、その名が示す通り、文字を扱う関数を提供する。また、文字列は文字のリストだから、文字列に対するフィルタリングやマッピングなどを助ける関数もこれに含まれる。

`Data.Char`は文字に対するpredicateを数多く提供してくれる。つまり、文字を受け取って、何かに基づいてその文字が`True`か`False`かを判定してくれる関数が、このモジュールには多数あるということだ。さっそく、見てみよう。

`isControl`は、その文字が制御文字か判定する。

`isSpace`は、その文字がスペースやタブ、開業などの空白文字かどうかを判定する。

`isLower`は、その文字が小文字か判定する。

`isUpper`は、その文字が大文字化判定する。

`isAlpha `は、その文字がアルファベットか判定する。

`isAlphaNum`は、その文字がアルファベットまたは数字かどうか判定する。

`isPrint`は、その文字が印刷可能か判定する。たとえば制御文字は印刷できない。

`isDigit`は、その文字が10進数の数字かどうか判定する。

`isOctDigit`は、その文字が8進数か判定する。

`isHexDigit`は、その文字が16進数か判定する。

`isLetter`は、その文字が英語を含めた文字化判定する。

`isMark`は、ユニコードの記号文字か判定する。これは、文字の前に付けられる記号であり、文字と組み合わせて使用されるものであり、、たとえばフランス語で使われる記号などがそれに該当する。

`isNumber`は、その文字が数値化どうか判定する。

    ghci> isDigit '5'
    True
    ghci> isDigit '５'
    False
    ghci> isNumber '5'
    True
    ghci> isNumber '５'
    True
    ghci> isNumber '五' --- これも5として扱ってほしい気がする
    False

`isPunctuation`は、その文字が句読点か判定する。

`isSymbol`は、その文字が、数学の記号化通貨の記号化を判定する。

`isSeparator`は、ユニコードの区切り文字か判定する。

`isAscii`は、ユニコードの文字セットの内、最初の128種類のASCII文字か判定する。

`isLatin1` は、その文字がユニコードの最初の256文字に該当するか判定する。

`isAsciiUpper` は、そのもじがASCII文字かつ大文字化判定する。

`isAsciiLower`は、その文字がASCII文字かつ小文字化判定する。

これらのpredicateは、すべて`Char -> Bool`というtype signatureとなっている。これらは、文字列などから特定の文字をフィルタリングするのに役立つ。たとえば、ユーザーネームを受け取るプログラムを作っていて、ユーザーネームとして使えるのは英数字のみだ、としよう。ユーザーネームが適切なものか、`Data.List`に含まれる関数と`Data.Char`に含まれるpredicateの組み合わせで判定することができる。

    ghci> all isAlphaNum "bobby283"
    True
    ghci> all isAlphaNum "eddy the fish!"
    False

クールだ! `all`は受け取ったリストの全ての要素に対してpredicateを満たすか判定する、というのは覚えておこう。

また、`isSpace` を使って、`Data.List`の`words`関数をシミュレートできる。

    ghci> words "hey guys its me"
    ["hey","guys","its","me"]
    ghci> groupBy ((==) `on` isSpace) "hey guys its me"
    ["hey"," ","guys"," ","its"," ","me"]

うーん、`words`のように機能しているけど、スペースが残ってしまった。さて、どうしよう? フィルターしよう。

    ghci> filter (not . any isSpace) . groupBy ((==) `on` isSpace) $ "hey guys its me"
    ["hey","guys","its","me"]

`Data.Char`は、`Ordering`のようなtypeも提供している。`Ordering`というtypeは、`LT, EQ, GT`という値を持つ。これは、ある種のenumerationだ。`LT, EQ, GT`というのは、2つの要素を比較した結果から発生しえる、答えの候補を示している。`GeneralCategory`というtypeも、enumerationの一種だ。これは、ある文字がどの種類に当てはまるか、というのを示している。そして、ある文字の種類を調べるための関数が、`generalCategory`となる。`generalCategory`のtypeは、　`generalCategory :: Char -> GeneralCategory`となる。`GeneralCategory`は、31種類もあるので、全ては紹介できないが、とりあえずこの関数を試してみよう。

    ghci> generalCategory ' '
    Space
    ghci> generalCategory 'A'
    UppercaseLetter
    ghci> generalCategory 'a'
    LowercaseLetter
    ghci> generalCategory '.'
    OtherPunctuation
    ghci> generalCategory '9'
    DecimalNumber
    ghci> map generalCategory " \t\nA9?|"
    [Space,Control,Control,UppercaseLetter,DecimalNumber,OtherPunctuation,MathSymbol]

`GeneralCategory`のtypeは,`Eq`というtypeclassの一部だから、`generalCategory c == Space`というような比較ができる。

`toUpper`は、スペース、数字などを除き、小文字を大文字へ変換する。

`toLower`は、大文字を小文字に変換する。

`toTitle`は、は、文字をtitle-caseに変換する。殆どの文字で、title-caseは大文字とおなじになる。

`digitToInt`は、文字を`Int`の数字に変換する。ただし、変換するには文字が`['0'..'9']`、`['a'..'f']`、`['A'..'F']`の範囲でなければならない。

    ghci> map digitToInt "34538"
    [3,4,5,3,8]
    ghci> map digitToInt "FF85AB"
    [15,15,8,5,10,11]

`intToDigit`は、`digitToInt`と逆の働きをする。`[0..15]`の範囲で数字を受け取り、`['0'..'f']`として結果を返す。

    ghci> intToDigit 15
    'f'
    ghci> intToDigit 5
    '5'

`ord`と`chr`は、文字をそれに対応した数字に、数字をそれに対応した文字に変換する。

    ghci> ord 'a'
    97
    ghci> chr 97
    'a'
    ghci> map ord "abcdefgh"
    [97,98,99,100,101,102,103,104]

`ord`で求めた値の差は、ある文字と文字がユニコード表上でどれだけ離れているかを示している。

Caesar cipherは、原始的なメッセージのエンコード方法であり、アルファベット上で、メッセージに含まれる文字を固定長だけずらす。もし対象の文字列がアルファベットに限るのであれば、Caesar cipherは、簡単に実装することができる。

    encode :: Int -> String -> String
    encode shift msg =
        let ords = map ord msg
            shifted = map (+ shift) ords
        in  map chr shifted


まず、文字列を数字のリストへ変換する。そして、数字のリストを文字列に戻す前に、シフト量をそれらの数字に加える。もし、あなたが関数合成オタクなら、`map (chr . (+ shift) . ord) msg`という書き方をするだろう。では、幾つかメッセージをエンコードしてみよう。

    ghci> encode 3 "Heeeeey"
    "Khhhhh|"
    ghci> encode 4 "Heeeeey"
    "Liiiii}"
    ghci> encode 1 "abcd"
    "bcde"
    ghci> encode 5 "Marry Christmas! Ho ho ho!"
    "Rfww~%Hmwnxyrfx&%Mt%mt%mt&"

問題なくエンコードで着ている。エンコードした文字列をデコードしたい場合は、エンコードするときに指定したシフト量だけ、逆方向にずらせばいい。これで、元の文字列が得られる。

    decode :: Int -> String -> String
    decode shift msg = encode (negate shift) msg

    ghci> encode 3 "Im a little teapot"
    "Lp#d#olwwoh#whdsrw"
    ghci> decode 3 "Lp#d#olwwoh#whdsrw"
    "Im a little teapot"
    ghci> decode 5 . encode 5 $ "This is a sentence"
    "This is a sentence"



## Data.Map

ディクショナリとも呼ばれる連想リストは、キーと値を保存するリストであり、リスト内の要素の順番は保証されない。たとえば、電話番号と持ち主の名前を、電話番号をキー、名前を値として保持する連想リストがあるとしよう。ここで重要なのは、リスト内の要素の順番ではなく、電話番号というキーから、持ち主の名前という値が正しく得られるうことだ。

Haskellで連想リストを表現する最も明確な方法は、ペアのリストを作る、という方法だ。ペアの1つめの要素がキー、2つめの要素が値となる。では、電話番号を使った連想リストの例を見てみよう。

    phoneBook =
        [("betty","555-2938")
        ,("bonnie","452-2928")
        ,("patsy","493-2928")
        ,("lucille","205-2928")
        ,("wendy","939-8282")
        ,("penny","853-2492")
        ]

この不格好なインデントで表現しているのは、単なるペアのリストだ。連想リストで重要なのは、キーから値が取り出せる、ということだ。では、キーを与えて値を取り出す関数を定義してみよう。

    findKey :: (Eq k) => k -> [(k,v)] -> v
    findKey key xs = snd . head . filter (\(k,v) -> key == k) $ xs

かなり単純だね。この関数は、キーとリストを受け取って、キーをもとにフィルターして、、キーが一致した要素のみが残る。そして、残った要素の内、最初の要素からキーを下に値を取り出し、それを返している。しかし、探しているキーが連想リスト内に存在しなかった場合、何が起こるだろう? うーむ。ここでは、空のリストに対して`head`を求めることになるので、ランタイムエラーが発生してしまう。しかし、簡単にプログラムのクラッシュを防ぐ方法を、我々は既に知っている。そう、Maybeを使うのだ。キーに一致する値が見つからなければ、`Nothing`を返す。見つかれば、それに対応する値を`Just その値`として返せばいい。

    findKey :: (Eq k) => k -> [(k,v)] -> Maybe v
    findKey key [] = Nothing
    findKey key ((k,v):xs) = if key == k
                                then Just v
                                else findKey key xs

type declarationを見てみよう。連想リストのキーを受け取って、値をMaybeに包んで返す。これは正しそうだ。これは、リストを扱う関数として、催奇のお手本のような関数だ。リストを先頭の要素と残りのリストに分割して、残りのリストにたいして関数を呼び出す。これは、典型的なfoldのパターンだ。では、これをfoldでどのように実装するのか見てみよう。

    findKey :: (Eq k) => k -> [(k,v)] -> Maybe v
    findKey key = foldr (\(k,v) acc -> if key == k then Just v else acc) Nothing

Note: 標準的なリストに対する催奇のパターンは、明示的に再起を書くのではなく、通常はfoldを使って書いたほうが良い。なぜなら、foldを使ったほうが、読みやすく理解しやすいからだ。foldが使われているのを見つけたら、それが実際には再帰しているというのが理解できる。

    vGghci> findKey "penny" phoneBook
    Just "853-2492"
    ghci> findKey "betty" phoneBook
    Just "555-2938"
    ghci> findKey "wilma" phoneBook
    Nothing

いい感じに動いている。もし女の子の電話番号があれば、その名前または`Nothing`が得られる。

さて、今実装した検索を行う関数は`Data.List`の機能を利用している。キーに対応する値を得るには、リストの要素すべてに対して、キーが見つかるまでトラバースをしなくてはいけない。`Data.Map`モジュールは、内部の実装が木構造となっているためより高速な連想リストを提供する。また、多くのユーティリティ関数も提供している。、ここからは、連想リストの代わりにマップを使うことに仕様。

`Data.Map`が提供する関数は、`Prelude`と`Data.List`モジュールと衝突するのでqualifiedインポートをしよう。

    import qualified Data.Map as Map

このインポート文をスクリプト内に書いてから、ghciでもう一度ロードする。

では、`Data.Map`に格納されている関数を概観してみよう。以下に、基本的な関数をまとめる。

`fromList`は、連想リストをリストとして受け取り、同じものを連想マップとして返す。

    ghci> Map.fromList [("betty","555-2938"),("bonnie","452-2928"),("lucille","205-2928")]
    fromList [("betty","555-2938"),("bonnie","452-2928"),("lucille","205-2928")]
    ghci> Map.fromList [(1,2),(3,4),(3,2),(5,5)]
    fromList [(1,2),(3,2),(5,5)]

元の連想リスト内に重複したキーが含まれていれば、そのキーは捨てられる。`fromList`のtype signatureは次のとおりだ。

    Map.fromList :: (Ord k) => [(k, v)] -> Map.Map k v

`k`と`v`というtypeのペアのリストを受け取り、`k`というtypeのキーに対して`v`というtypeの値をマッピングした、マップを返す。普通のリストを連想リストとして扱う場合はそのキーは等しさが確かめられさえすれば、(`Eq`というtypeclassに属してさえいれば)問題なかった。しかし、マップのキーは`Ord`にも属している必要がある。これは、`Data.Map`モジュールに不可欠な製薬となる。キーが順序を持つことで、それらを木構造へと整形できるのだ。


キーのtypeが`Ord`に属していないとき以外は、キーと値の組合せに対して常に`Data.Map`を使うほうがいいだろう。

`empty`は、空のマップを表現する。これは、引数を取らず、空のマップを返す。

    ghci> Map.empty
    fromList []

`insert`は、キー、値、そしてマップを受け取り、元のマップの構造を保ったまま、キーと値が挿入された新しいマップを返す。

    ghci> Map.empty
    fromList []
    ghci> Map.insert 3 100 Map.empty
    fromList [(3,100)]
    ghci> Map.insert 5 600 (Map.insert 4 200 ( Map.insert 3 100  Map.empty))
    fromList [(3,100),(4,200),(5,600)]
    ghci> Map.insert 5 600 . Map.insert 4 200 . Map.insert 3 100 $ Map.empty
    fromList [(3,100),(4,200),(5,600)]

さて、独自の`fromList`を`insert`と空のマップ、そしてfoldを使って実装することができる。見てみよう。

    fromList' :: (Ord k) => [(k,v)] -> Map.Map k v
    fromList' = foldr (\(k,v) acc -> Map.insert k v acc) Map.empty

このfoldは実に直感的だ。まず、空のマップから開始して、リストの右からキーと値のペアを`ac`として取り出し、それをマップへと挿入しながら畳み込んでいく。

`null`は、マップが空のマップか判定する。

    ghci> Map.null Map.empty
    True
    ghci> Map.null $ Map.fromList [(2,3),(5,5)]
    False

`size`は、マップの大きさを返す。

    ghci> Map.size Map.empty
    0
    ghci> Map.size $ Map.fromList [(2,4),(3,3),(4,2),(5,4),(6,4)]
    5
`singleton`は、キーと値を受け取り、1つの要素のみを持つマップを返す。

    ghci> Map.singleton 3 9
    fromList [(3,9)]
    ghci> Map.insert 5 9 $ Map.singleton 3 9
    fromList [(3,9),(5,9)]

`lookup`は、`Data.List`の`lookup`のように機能するが、こちらはマップに対してのみ機能し、もし目的の値が見つかれば`Just その値`、見つからなければ`Nothing`を返す。

`member`は、キーとマップを受け取り、そのマップ内に目的のキーが存在するか判定するpredicateだ。

    ghci> Map.member 3 $ Map.fromList [(3,6),(4,3),(6,9)]
    True
    ghci> Map.member 3 $ Map.fromList [(2,5),(4,5)]
    False

`map`と`filter`は、リストに対するそれと同じように機能する。

    ghci> Map.map (*100) $ Map.fromList [(1,1),(2,4),(3,9)]
    fromList [(1,100),(2,400),(3,900)]
    ghci> Map.filter isUpper $ Map.fromList [(1,'a'),(2,'A'),(3,'b'),(4,'B')]
    fromList [(2,'A'),(4,'B')]

`toList`は、`fromList`と逆の働きをする。

    ghci> Map.toList . Map.insert 9 2 $ Map.singleton 4 3
    [(4,3),(9,2)]

`keys`と`elems`は、それぞれキーと値のリストをを取り出す。これは、それぞれマップに対して[`fst . Map.toList`と`snd . Map.toList`を求めるのと等しい。

`fromListWith`は、ちょっとおもしろい関数だ。これは`fromList`のように機能するが、こちらは重複した要素を捨てることなく渡した関数によってその重複をどう扱うかを決めることができる。では、女の子が複数の電話番号を持てるように、連想リストを次のようにセットアップしてみよう。

    phoneBook =
        [("betty","555-2938")
        ,("betty","342-2492")
        ,("bonnie","452-2928")
        ,("patsy","493-2928")
        ,("patsy","943-2929")
        ,("patsy","827-9162")
        ,("lucille","205-2928")
        ,("wendy","939-8282")
        ,("penny","853-2492")
        ,("penny","555-2111")

さて、ここで通常の`fromList`を使うと、いくつかの電話番号が失われてしまう。そこで、以下のようにする。        

    phoneBookToMap :: (Ord k) => [(k, String)] -> Map.Map k String
    phoneBookToMap xs = Map.fromListWith (\number1 number2 -> number1 ++ ", " ++ number2) xs

    ghci> Map.lookup "patsy" $ phoneBookToMap phoneBook
    "827-9162, 943-2929, 493-2928"
    ghci> Map.lookup "wendy" $ phoneBookToMap phoneBook
    "939-8282"
    ghci> Map.lookup "betty" $ phoneBookToMap phoneBook
    "342-2492, 555-2938"

もし重複が見つかれば、渡した関数がそれらの値を受け取り、別の値へと結合する。また、最初に連想リストのすべての値をシングルトンリストにしてから、それらを`++`で結合することもできる。

    phoneBookToMap :: (Ord k) => [(k, a)] -> Map.Map k [a]
    phoneBookToMap xs = Map.fromListWith (++) $ map (\(k,v) -> (k,[v])) xs

    ghci> Map.lookup "patsy" $ phoneBookToMap phoneBook
    ["827-9162","943-2929","493-2928"]

美しい。他の用途としては、数値を含む連想リストからマップを作る際に重複が見つかった場合、重複の中から最も大きい値のみを残す、というのが挙げられ得る。

    ghci> Map.fromListWith max [(2,3),(2,5),(2,100),(3,29),(3,22),(3,11),(4,22),(4,15)]
    fromList [(2,100),(3,29),(4,22)]

あるいは、同じキーに対しては、その値を足し合わせる、という用途もある。

    ghci> Map.fromListWith (+) [(2,3),(2,5),(2,100),(3,29),(3,22),(3,11),(4,22),(4,15)]
    fromList [(2,108),(3,62),(4,37)]

`insertWith`は、`fromList`に対する`fromListWith`のようなものだ。この関数はキーと値のペアをマップへと挿入するが、すでにマップ内に重複が存在する場合の判断は、渡された関数によって決めることができる。

    ghci> Map.insertWith (+) 3 100 $ Map.fromList [(3,4),(5,103),(6,339)]
    fromList [(3,104),(5,103),(6,339)]

これらは、`Data.Map`に含まれるほんの一部の関数でしかないから、すべての関数について知りたい場合はドキュメントを参照してほしい。



## Data.Set

`Data.Set`モジュールは、集合に関係するものを提供する。集合というのは、数学における集合だ。集合はリストとマップが交わったようなものだ。集合に含まれる要素には重複がない。また、その要素は内部的には木構造で実装されているので(`Data.Map`のマップのように)順序を持つ。集合に対する要素の挿入や削除などは、リストに対するそれより高速に行われる。集合に対する一般的な操作としては、要素の挿入や要素が集合に含まれるかの判定、集合からリストへの変換などがある。

`Data.Set`に含まれる名前は`Prelude`や`Data.List`と衝突するのでqualifiedインポートする。

スクリプトに以下の`import`文を追加しよう。

    import qualified Data.Set as Set

それから、ghciで再読込しよう。

さて、ある2つの文章から、どちらにも含まれる部分を抽出するとしよう。

    text1 = "I just had an anime dream. Anime... Reality... Are they so different?"
    text2 = "The old man left his garbage can out and now his trash is all over my lawn!"

`fromList`は、期待どおりの機能をする。これは、リストから集合へと変換する。

    ghci> let set1 = Set.fromList text1
    ghci> let set2 = Set.fromList text2
    ghci> set1
    fromList " .?AIRadefhijlmnorstuy"
    ghci> set2
    fromList " !Tabcdefghilmnorstuvwy"

ご覧のとおり、それぞれの要素には重複がなく、また、順序がある。では、`intersection`関数を使って、両方の集合が共有している要素を取り出してみよう。

    ghci> Set.intersection set1 set2
    fromList " adefhilmnorstuy"

`difference`関数は、2津の集合の内、1爪には存在するが、2つめには存在しない、また、その逆も満たす要素を返す。

    ghci> Set.difference set1 set2
    fromList ".?AIRj"
    ghci> Set.difference set2 set1
    fromList "!Tbcgvw"

また、どちらの文章にも存在する要素を重複なく取り出すには`union`を使う。

    ghci> Set.union set1 set2
    fromList " !.?AIRTabcdefghijlmnorstuvwy"

`null, size, member, empty, singleton, insert, delete`、これらの関数は期待通りの動作をする。

    ghci> Set.null Set.empty
    True
    ghci> Set.null $ Set.fromList [3,4,5,5,4,3]
    False
    ghci> Set.size $ Set.fromList [3,4,5,3,4,5]
    3
    ghci> Set.singleton 9
    fromList [9]
    ghci> Set.insert 4 $ Set.fromList [9,3,8,1]
    fromList [1,3,4,8,9]
    ghci> Set.insert 8 $ Set.fromList [5..10]
    fromList [5,6,7,8,9,10]
    ghci> Set.delete 4 $ Set.fromList [3,4,5,4,3,4,5]
    fromList [3,5]

そして、ある集合の部分集合や真部分集合を調べることもできる。たとえば、集合Aは集合Bの部分集合であるとき、集合Aに含まれるすべての要素は集合Bにも含まれる。また、集合Bは集合Aのすべての要素に加えて集合Aがとり得る要素の組み合わせも含むとき、集合Aは集合Bの真部分集合となる。


    ghci> Set.fromList [2,3,4] `Set.isSubsetOf` Set.fromList [1,2,3,4,5]
    True
    ghci> Set.fromList [1,2,3,4,5] `Set.isSubsetOf` Set.fromList [1,2,3,4,5]
    True
    ghci> Set.fromList [1,2,3,4,5] `Set.isProperSubsetOf` Set.fromList [1,2,3,4,5]
    False
    ghci> Set.fromList [2,3,4,8] `Set.isSubsetOf` Set.fromList [1,2,3,4,5]
    False

もちろん集合に対しても`map`や`filter`が使える。

    ghci> Set.filter odd $ Set.fromList [3,4,5,6,7,2,3,4]
    fromList [3,5,7]
    ghci> Set.map (+1) $ Set.fromList [3,4,5,6,7,2,3,4]
    fromList [3,4,5,6,7,8]

集合は、リストの重複を取り除くのによく使われ、一旦イストを集合へ変換した後重複を取り除き、再度リストへへんかんすることで、目的のリストが得られる。すでに、`Data.List`の`nub`がその機能を担っているが、巨大なリストに対しては、一旦リストを集合へ変換した後、重複を取り除いてからリストへ要素を詰め込むほうが、`nub`を使うよりも高速に処理できる。ただし、`nub`で重複を取り除きたい場合は、リスト内の要素が`Eq`というtypeclassに属してさえいればいいのに大し、集合を使って重複を取り除く場合は、`Ord`にも属している必要がある。

    ghci> let setNub xs = Set.toList $ Set.fromList xs
    ghci> setNub "HEY WHATS CRACKALACKIN"
    " ACEHIKLNRSTWY"
    ghci> nub "HEY WHATS CRACKALACKIN"
    "HEY WATSCRKLIN"

`setNub`は、巨大なリストに対しては概ね高速に動作するが、先程確認したとおり、`nub`は処理後の要素の順番が保たれるのに対して、`setNub`は保たれない。



## Making our own modules - 独自のモジュールを作ろう

ここまで、クールなモジュールをいろいろ見てきたが、独自のモジュールを作りたくなった場合はどうするんだろう? 多くのプログラミング言語では、プログラムを小さなプログラムへ分割することができるが、Haskellもそれと違いはない。同じような目的で使用する関数やtypeclassなどを作った場合は、それをモジュールにまとめるのが一般的に有効な手段だ。そして、そのモジュールを別のプログラムでインポートすることで、使い回しができる。[

早速、体積と面積を求めたりするちょっとした関数が格納された独自のモジュールをどのように作るのか見てみよう。まず、`geometry.hs`というファイルを作るところからはじめよう。

関数をエクスポートするモジュールといった場合、インポートすればエクスポートされた関数が仕様できる、というのを意味する。モジュール内で呼び出される関数を定義できもちろんモジュールの内部でのみ呼び出される関数が定義できるし、外部から見えるのは、一度エクスポートされた関数のみとなる。

まず、モジュールはその名前を指定するところから始める。`geometry.hs`というファイル名に対しては、`Geometry`というモジュール名を指定しよう。そして、どの関数をエクスポートするかを指定してから関数を書き始める。つまり、以下のように始める。

    module Geometry
    ( sphereVolume
    , sphereArea
    , cubeVolume
    , cubeArea
    , cuboidArea
    , cuboidVolume
    ) where

ご覧のとおり、急、立方体、直方体、それぞれの表面積と堆積を求める関数をエクスポートしている。では、それらの関数がどう定義されるか見てみよう。

    module Geometry
    ( sphereVolume
    , sphereArea
    , cubeVolume
    , cubeArea
    , cuboidArea
    , cuboidVolume
    ) where
    
    sphereVolume :: Float -> Float
    sphereVolume radius = (4.0 / 3.0) * pi * (radius ^ 3)
    
    sphereArea :: Float -> Float
    sphereArea radius = 4 * pi * (radius ^ 2)
    
    cubeVolume :: Float -> Float
    cubeVolume side = cuboidVolume side side side
    
    cubeArea :: Float -> Float
    cubeArea side = cuboidArea side side side
    
    cuboidVolume :: Float -> Float -> Float -> Float
    cuboidVolume a b c = rectangleArea a b * c
    
    cuboidArea :: Float -> Float -> Float -> Float
    cuboidArea a b c = rectangleArea a b * 2 + rectangleArea a c * 2 + rectangleArea c b * 2
    
    rectangleArea :: Float -> Float -> Float
    rectangleArea a b = a * b

標準的な方法を挙げてみた。ここで、幾つか注目すべきことがある。立方体は、直方体の特殊な場合だから、すべての辺の長さが等しい直方体として定義している。また、`rectangleAre`という名前の長方形の面積をそのへんの長さに基づいて計算するヘルパー関数も定義している。これは、ただの乗算だから取るに足りない関数だ。この関数は、モジュール内の`cuboidArea`と`cuboidVolume`の中で使用されているが、外部にエクスポートはされていない。このモジュールがエクスポートしたいのは、3次元の物体を扱う関数だけだから、`rectangularArea`をエクスポートする必要はない。

通常、モジュールを作成するときに外部へエクスポートされるのはインターフェースとなる関数だけだから、その内部の実装は隠される。誰かが今作成したモジュールを使う場合も、エクスポートされていない関数について考える必要はない。たとえば、今作成したモジュールの内部で使われている関数を削除したり、完全に新しい物へ書き換えることもでき、`rectangularArea`をただの`*`に書き換えることもできる。そして、この関数はエクスポートされていないから、誰もこの関数について心配する必要はないのだ。

さて、作成したモジュールを使うには、以下のようにする。

    import Geometry

`geometry.hs`は、それをインポートするプログラムが置かれているディレクトリと同じ場所に配置しなければならない。

モジュールは階層的な構造にすることもできる。それぞれのモジュールがサブモジュールを持つことができ、そのサブモジュールもそれぞれのサブモジュールを持つことができる。早速、先ほどのGeometryモジュールを3種類の物体に対するサブモジュールへと切り分けてみよう。

まず、`Geometry`という名前のディレクトリを作成する。名前は大文字ではじめるのを忘れずに。そして、`Sphere.hs, Cuboid.hs, Cube.hs`という3種類のファイルをそのディレクトリ内に作成する。それぞれのファイルの中身は、こうなる。

* `Sphere.hs`

    module Geometry.Sphere
    ( volume
    , area
    ) where
    
    volume :: Float -> Float
    volume radius = (4.0 / 3.0) * pi * (radius ^ 3)
    
    area :: Float -> Float
    area radius = 4 * pi * (radius ^ 2)



* `Cuboid.hs`

    module Geometry.Cuboid
    ( volume
    , area
    ) where
    
    volume :: Float -> Float -> Float -> Float
    volume a b c = rectangleArea a b * c
    
    area :: Float -> Float -> Float -> Float
    area a b c = rectangleArea a b * 2 + rectangleArea a c * 2 + rectangleArea c b * 2
    
    rectangleArea :: Float -> Float -> Float
    rectangleArea a b = a * b



* `Cube.hs`

    module Geometry.Cube
    ( volume
    , area
    ) where
    
    import qualified Geometry.Cuboid as Cuboid
    
    volume :: Float -> Float
    volume side = Cuboid.volume side side side
    
    area :: Float -> Float
    area side = Cuboid.area side side side

以上! まずは`Geometry.Sphere`から見てみよう。まず`Geometry`というディレクトリを作ってから、モジュールを`Geometry.Sphere`と定義しているのに注目しよう。同じことを`Cuboid`にも行う。また、それぞれのサブモジュール内で同じ名前の関数を定義していることにも注目してほしい。これが可能なのは、それぞれのモジュールが分離されているからだ。しかし、`Geometry.Cube`から`Geometry.Cuboid`ないにある関数を使いたくても、`Geometry.Cube`と`Geometry.Cuboid`内にある関数は、名前がおなじなので直接インポートすることはできない。常にqualifiedインポートをしたほうが良いのは、これが理由だ。

そこで、`Geometry`ディレクトリ内にある、つまり同じ階層にあるモジュールでは以下のようなことができる。

    import Geometry.Sphere

そして、、`area`や`volume`を呼び出せば、球体の表面積や体積が得られる。また、複数のモジュールを同時にうまく扱いたい場合は、それらがエクスポートしている関数の名前が同じなので、qualifiedインポートをする必要がある。つまり、以下のようにする。

    import qualified Geometry.Sphere as Sphere
    import qualified Geometry.Cuboid as Cuboid
    import qualified Geometry.Cube as Cube

これで、`Sphere.area`で球体の表面積が、`Sphere.volume`で球体の体積が、`Cuboid.area`で直方体の表面積...などなど、それぞれの物体に応じた表面積や体積が得られるようになった。

これからは、大量の関数が格納された巨大なスクリプトを作成するときは同じような目的の関数がないかを調べて、モジュールにまとめてみよう。そうすれば、自戒からは自分のプログラムで同じような機能が必要になったとき、自作したモジュールをインポートするだけで済むようになる。

